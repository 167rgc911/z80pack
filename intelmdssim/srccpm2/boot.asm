; INTEL MDS CP/M BOOT SECTOR DISASSEMBLY

	ORG	3000H		; BOOTSTRAP ROM LOADS AT THIS ADDRESS

BIOS	EQU	0F000H		; BASIC INPUT/OUTPUT SYSTEM

;	MDS MONITOR EQUATES

RMON80	EQU	0FF0FH		; RESTART MON80 (BOOT ERROR)

;	I/O EQUATES

RTC	EQU	0FFH		; REAL TIME CLOCK PORT
BOOT	EQU	00000010B	; BOOTSTRAP MODE INDICATOR, 1 = ON

;	202 CONTROLLER EQUATES

BASE	EQU	78H		; BASE OF DISK COMMAND IO PORTS
DSTAT	EQU	BASE		; DISK STATUS (INPUT)
RTYPE	EQU	BASE+1		; RESULT TYPE (INPUT)
RBYTE	EQU	BASE+3		; RESULT BYTE (INPUT)

ILOW	EQU	BASE+1		; IOPB LOW ADDRESS (OUTPUT)
IHIGH	EQU	BASE+2		; IOPB HIGH ADDRESS (OUTPUT)
RESET	EQU	BASE+7		; RESET CONTROLLER (OUTPUT)

READF	EQU	4H		; READ FUNCTION
IORDY	EQU	00000100B	; I/O FINISHED MASK

;	CODE SECTION

START:	LXI	SP,100H		; INITIALIZE STACK POINTER
	IN	RTYPE		; FINISH BOOTSTRAP ROM DISK I/O
	IN	RBYTE		; BY READING RESULT TYPE AND BYTE

BSLOOP:	IN	RTC		; LOOP UNTIL BOOTSTRAP MODE IS DISABLED
	ANI	BOOT
	JNZ	BSLOOP

	OUT	RESET		; RESET DISK CONTROLLER
	MVI	B,2		; NUMBER OF IOPB'S TO EXECUTE
	LXI	H,IOPBS		; LOAD FIRST IOPB ADDRESS

LDLOOP:	MOV	A,L
	OUT	ILOW		; LOW ADDRESS TO CONTROLLER
	MOV	A,H
	OUT	IHIGH		; HIGH ADDRESS AND START I/O

WAIT:	IN	DSTAT		; WAIT FOR COMPLETION
	ANI	IORDY
	JZ	WAIT

	IN	RTYPE		; READ RESULT TYPE
	ANI	00000011B	; MASK TYPE
	CPI	2		; IF NOT I/O COMPLETE RESTART
	JNC	START

	IN	RBYTE		; READ RESULT BYTE
	RAL
	CC	RMON80		; UNIT NOT READY, RESTART SYSTEM
	RAR
	ANI	00011110B	; ANY OTHER ERRORS -> RESTART
	JNZ	START

	LXI	D,7		; ADVANCE TO NEXT IOPB
	DAD	D
	DCR	B		; DONE?
	JNZ	LDLOOP

	JMP	BIOS		; JUMP TO BIOS

; LOADS 59 SECTORS FROM T0/S2 TO T1/S8 INTO DA00-F77F

IOPBS:	DB	80H		; NORMAL I/O OPERATION
	DB	READF		; IO FUNCTION, READ
	DB	51		; NUMBER OF SECTORS TO READ
	DB	0		; TRACK NUMBER
	DB	2		; SECTOR NUMBER
	DW	0DA00H		; IO ADDRESS

	DB	80H		; NORMAL I/O OPERATION
	DB	READF		; IO FUNCTION, READ
	DB	8		; NUMBER OF SECTORS TO READ
	DB	1		; TRACK NUMBER
	DB	1		; SECTOR NUMBER
	DW	0F380H		; IO ADDRESS

	END
